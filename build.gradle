plugins {
  id 'java'
  id 'maven-publish'
  id 'eclipse'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'eclipse'
  
	repositories {
	  mavenCentral()
	}
	
  java {
    withJavadocJar()
    withSourcesJar()
  }
  
  dependencies {
    testImplementation 'org.mockito:mockito-core:4.1.0'
  }
  
  testing {
    suites {
      test {
        useJUnitJupiter('5.8.2')
      }
    }
  }
  
  publishing {
    repositories {
      maven {
        name = 'GitHubPackages'
        url = 'https://maven.pkg.github.com/picatrix1899/'
        credentials {
          username = System.getenv('USERNAME')
          password = System.getenv('TOKEN')
        }
      }
    }

    publications {
      mavenJava(MavenPublication) {
        pom {
          url = 'https://github.com/picatrix1899/barghos/'

          licenses {
            license {
              name = 'MIT'
              url = 'https://github.com/picatrix1899/barghos/blob/master/LICENSE'
              distribution = 'repo'
            }
          }

          developers {
            developer {
              id = 'picatrix1899'
              name = 'Florian Zilkenat (picatrix1899)'
            }
          }
        }
      }
    }
  }
}
