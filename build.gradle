subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'eclipse'
  
  repositories {
    mavenCentral()
  }
  
  dependencies {
    testImplementation 'org.mockito:mockito-core:4.1.0'
  }
  
  java {
    withJavadocJar()
    withSourcesJar()
	
	sourceCompatibility = '17'
    targetCompatibility = '17'
  }
  
  testing {
    suites {
      test {
        useJUnitJupiter('5.8.2')
      }
    }
  }
  
  compileJava {
    modularity.inferModulePath = true
	
	options.encoding = "UTF-8"
	
	sourceCompatibility = '17'
    targetCompatibility = '17'
  }
  
  compileTestJava {
	modularity.inferModulePath = true
	
	options.encoding = "UTF-8"
	
	sourceCompatibility = '17'
    targetCompatibility = '17'
  }
  
  javadoc {
  
  	// Enable more javadoc tags.
  	options.addMultilineStringsOption('tag').setValue([
  	
  	  // Enable the javadoc tags @apiNote, @implSpec and @implNote for better documentation as they are not enabled by default.
  	  'apiNote:a:"API Note:"',
  	  'implSpec:a:"Implementation Requirements:"',
  	  'implNote:a:"Implementation Note:"'
  	])
  }
  
  eclipse {
    project {
	  natures 'org.eclipse.buildship.core.gradleprojectnature'
	
	  buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
    }
  
    jdt {
      sourceCompatibility = '17'
      targetCompatibility = '17'
      javaRuntimeName = 'JavaSE-17'
    }
  
    classpath {
      downloadSources = true
      downloadJavadoc = true
    }
  }
  
  publishing {
    repositories {
      maven {
        name = 'GitHubPackages'
        url = 'https://maven.pkg.github.com/picatrix1899/barghos'
        credentials {
          username = System.getenv('USERNAME')
          password = System.getenv('TOKEN')
        }
      }
    }

    publications {
      mavenJava(MavenPublication) {
        pom {
          url = 'https://github.com/picatrix1899/barghos/'

          licenses {
            license {
              name = 'MIT'
              url = 'https://github.com/picatrix1899/barghos/blob/master/LICENSE'
              distribution = 'repo'
            }
          }

          developers {
            developer {
              id = 'picatrix1899'
              name = 'Florian Zilkenat (picatrix1899)'
            }
          }
        }
      }
    }
  }
}
